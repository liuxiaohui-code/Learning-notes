# 命令

```go
// main.go
package main

var Id = 9527
```

```sh
# 查看的Go语言程序对应的伪汇编代码,会生成.o文件
go tool compile \
  -S \  # 输出汇编格式
  main.go
# 输出
go.cuinfo.packagename. SDWARFCUINFO dupok size=0
        0x0000 6d 61 69 6e                                      main
""..inittask SNOPTRDATA size=24
        0x0000 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00  ................
        0x0010 00 00 00 00 00 00 00 00                          ........
"".Id SNOPTRDATA size=8
        0x0000 37 25 00 00 00 00 00 00                          7%......
# 其中"".Id对应Id变量符号,变量的内存大小为8个字节,变量的初始化内容为37 25 00 00 00 00 00 00，对应十六进制格式的0x2537，对应十进制为9527,SNOPTRDATA是相关的标志，其中NOPTR表示数据中不包含指针数据。
```

## DATA 变量初始化

Go 汇编语言提供了 DATA 命令用于初始化包变量，DATA 命令的语法如下：

```go
DATA symbol+offset(SB)/width, value
// 其中symbol为变量在汇编语言中对应的标识符，offset是符号开始地址的偏移量，width是要初始化内存的宽度大小，value是要初始化的值。其中当前包中Go语言定义的符号symbol，在汇编代码中对应·symbol，其中“·”中点符号为一个特殊的unicode符号。

//给Id变量初始化为十六进制的0x2537，对应十进制的9527（常量需要以美元符号$开头表示
DATA ·Id+0(SB)/1,$0x37
DATA ·Id+1(SB)/1,$0x25

```

## GLOBL 变量导出

```go
// 变量定义好之后需要导出以供其它代码引用。Go汇编语言提供了GLOBL命令用于将符号导出：
GLOBL symbol(SB), width
// 其中symbol对应汇编中符号的名字，width为符号对应内存的大小。
GLOBL ·Id, $8
```

## 定义字符串变量

```go
GLOBL ·NameData(SB),NOPTR,$8 // 底层字符串
DATA  ·NameData(SB)/8,$"gopher"
GLOBL ·STR(SB),RODATA,$16 // 只读 string
DATA  ·STR+0(SB)/8,$·NameData(SB) // 引用底层字符串
DATA  ·STR+8(SB)/8,$6 // 字符串长度

// 不导出底层字符数组
GLOBL ·Name(SB),$24
DATA ·Name+0(SB)/8,$·Name+16(SB)
DATA ·Name+8(SB)/8,$6
DATA ·Name+16(SB)/8,$"gopher"

//在go中声明
var STR string

println(STR)
```

## 定义 main 函数

Go 语言函数在函数调用时，完全通过栈传递调用参数和返回值.

```go
TEXT ·main(SB), $16-0 //用于定义main函数，其中$16-0表示main函数的帧大小是16个字节,0表示main函数没有参数和返回值
  MOVQ ·helloworld+0(SB), AX; MOVQ AX, 0(SP) //先通过MOVQ指令，将helloworld对应的字符串头部结构体的16个字节复制到栈指针SP对应的16字节的空间
  MOVQ ·helloworld+8(SB), BX; MOVQ BX, 8(SP)
  CALL runtime·printstring(SB) //打印字符串
  CALL runtime·printnl(SB)    // 打印换行符
  RET //返回
```

# 寄存器

真寄存器

- `IP`
- `AX`
- `BX`
- `CX`
- `DX`
- `SI`
- `DI`
- `BP`
- `SP`:对应的是栈的顶部,一般用于定位调用其它函数的参数和返回值,一般没有标识符前缀
- `R8~R15`

伪寄存器: Go 汇编为了简化汇编代码的编写，引入了 PC、FP、SP、SB 四个伪寄存器。四个伪寄存器加其它的通用寄存器就是 Go 汇编语言对 CPU 的重新抽象;伪寄存器一般需要一个标识符和偏移量为前缀

- `PC`:其实是 IP 指令计数器寄存器的别名
- `FP`:对应的是函数的帧指针，一般用来访问函数的参数和返回值
- `SP`:对应的是当前函数栈帧的底部（不包括参数和返回值部分），一般用于定位局部变量
- `SB`

# 常见汇编指令

MOV 指令可以用于将字面值移动到寄存器、字面值移到内存、寄存器之间的数据传输、寄存器和内存之间的数据传输。

```go
// 指令格式
`MOVQ` BX, 8(SP) // 将通用寄存器8字节数据传到真寄存器SP中

[1]byte	`MOVB`	//移动一个字节
[2]byte	`MOVW`
[4]byte	`MOVL`
[8]byte	`MOVQ`
// 数据扩展与截断,得到数据类型-386-amd
int8	`MOVBLSX`	`MOVBQSX`
uint8	`MOVBLZX`	`MOVBQZX`
int16	`MOVWLSX`	`MOVWQSX`
uint16	`MOVWLZX`	`MOVWQZX`
// 基础算数
`ADD`	加法
`SUB`	减法
`MUL`	乘法
`DIV`	除法
`AND`	逻辑与
`OR`	逻辑或
`NOT`	逻辑取反
// 流程控制
`JMP`	无条件跳转
`JMP-if-x`	有条件跳转，JL、JLZ、JE、JNE、JG、JGE
`CALL`	调用函数
`RET`	函数返回
// 其他
`LEA`	取地址,将标准参数格式中的内存地址加载到寄存器
`PUSH`	压栈,用寄存器中的SP为栈指针，栈是向低地址方向增长的
`POP`	出栈
```

最后需要说明的是，Go 汇编语言可能并没有支持全部的 CPU 指令。如果遇到没有支持的 CPU 指令，可以通过 Go 汇编语言提供的 BYTE 命令将真实的 CPU 指令对应的机器码填充到对应的位置。完整的 X86 指令在 https://github.com/golang/arch/blob/master/x86/x86.csv 文件定义。同时 Go 汇编还正对一些指令定义了别名，具体可以参考这里 https://golang.org/src/cmd/internal/obj/x86/anames.go 。

# 常量与全局变量

建议使用 go 语言进行定义

## 常量

Go 汇编语言中常量以$美元符号为前缀。常量的类型有整数常量、浮点数常量、字符常量和字符串常量等几种类型。

```go
$1           // 十进制
$0xf4f8fcff  // 十六进制
$1.5         // 浮点数
$'a'         // 字符
$"abcd"      // 字符串
// 对于数值型常量，可以通过常量表达式构成新的常量
$2+2      // 常量表达式
$3&1<<2   // == $4
$(3&1)<<2 // == $4
// Go汇编语言中的常量其实不仅仅只有编译时常量，还包含运行时常量。比如包中全局的变量和全局函数在运行时地址也是固定不变的，这里地址不会变量的包变量和函数的地址也是一种汇编常量。
DATA  ·Name+0(SB)/8,$·NameData(SB)
```

## 全局变量

```go
//1. 声明变量名symbol,大小
GLOBL symbol(SB), width
GLOBL ·count(SB),$4 // 4字节,其中符号·count以中点开头表示是当前包的变量，最终符号名为被展开为path/to/pkg.count,内存的宽度必须是2的指数倍
//2. 赋值,具体的含义是从symbol+offset偏移量开始，width宽度的内存，用value常量对应的值初始化。DATA初始化内存时，width必须是1、2、4、8几个宽度之一，因为再大的内存无法一次性用一个uint64大小的值表示。
DATA symbol+offset(SB)/width, value
DATA ·count+0(SB)/1,$1
DATA ·count+1(SB)/1,$2
DATA ·count+2(SB)/1,$3
DATA ·count+3(SB)/1,$4
// or
DATA ·count+0(SB)/4,$0x04030201
```

## 数组类型

```go
var num [2]int
// 定义一个对应16字节大小的变量，并用零值进行初始化
GLOBL ·num(SB),$16
DATA ·num+0(SB)/8,$0
DATA ·num+8(SB)/8,$0
```

## bool 型变量

bool 类型的内存大小为 1 个字节。并且汇编中定义的变量需要手工指定初始化值，否则将可能导致产生未初始化的变量。当需要将 1 个字节的 bool 类型变量加载到 8 字节的寄存器时，需要使用 MOVBQZX 指令将不足的高位用 0 填充。

```go
var (
    boolValue  bool
    trueValue  bool
    falseValue bool
)

GLOBL ·boolValue(SB),$1   // 未初始化

GLOBL ·trueValue(SB),$1   // var trueValue = true
DATA ·trueValue(SB)/1,$1  // 非 0 均为 true

GLOBL ·falseValue(SB),$1  // var falseValue = true
DATA ·falseValue(SB)/1,$0
```

## int 型变量

汇编定义变量时初始化数据并不区分整数是否有符号。只有在 CPU 指令处理该寄存器数据时，才会根据指令的类型来取分数据的类型或者是否带有符号位。

```go
var int32Value int32
var uint32Value uint32

GLOBL ·int32Value(SB),$4
DATA ·int32Value+0(SB)/1,$0x01  // 第0字节
DATA ·int32Value+1(SB)/1,$0x02  // 第1字节
DATA ·int32Value+2(SB)/2,$0x03  // 第3-4字节

GLOBL ·uint32Value(SB),$4
DATA ·uint32Value(SB)/4,$0x01020304 // 第1-4字节
```

## float 型变量

Go 汇编语言通常无法区分变量是否是浮点数类型，与之相关的浮点数机器指令会将变量当作浮点数处理。Go 语言的浮点数遵循 IEEE754 标准，有 float32 单精度浮点数和 float64 双精度浮点数之分。

IEEE754 标准中，最高位 1bit 为符号位，然后是指数位（指数为采用移码格式表示），然后是有效数部分（其中小数点左边的一个 bit 位被省略）。

IEEE754 浮点数还有一些奇妙的特性：比如有正负两个 0；除了无穷大和无穷小 Inf 还有非数 NaN；同时如果两个浮点数有序那么对应的有符号整数也是有序的（反之则不一定成立，因为浮点数中存在的非数是不可排序的）。浮点数是程序中最难琢磨的角落，因为程序中很多手写的浮点数字面值常量根本无法精确表达，浮点数计算涉及到的误差舍入方式可能也的随机的。

```go
var float32Value float32

var float64Value float64

GLOBL ·float32Value(SB),$4
DATA ·float32Value+0(SB)/4,$1.5      // var float32Value = 1.5

GLOBL ·float64Value(SB),$8
DATA ·float64Value(SB)/8,$0x01020304 // bit 方式初始化
```

## string 类型变量

```go
type reflect.StringHeader struct {
    Data uintptr
    Len  int
}
var helloworld string

GLOBL ·helloworld(SB),$16
//定义了一个text当前文件内的私有变量（以<>为后缀名）,虽然text<>私有变量表示的字符串只有12个字符长度，但是我们依然需要将变量的长度扩展为2的指数倍数，这里也就是16个字节的长度。其中NOPTR表示text<>不包含指针数据。
GLOBL text<>(SB),NOPTR,$16
DATA text<>+0(SB)/8,$"Hello Wo"
DATA text<>+8(SB)/8,$"rld!"

DATA ·helloworld+0(SB)/8,$text<>(SB) // StringHeader.Data
DATA ·helloworld+8(SB)/8,$12         // StringHeader.Len
```

## slice 类型变量

```go
type reflect.SliceHeader struct {
    Data uintptr
    Len  int
    Cap  int
}

var helloworld []byte

GLOBL ·helloworld(SB),$24            // var helloworld []byte("Hello World!")
DATA ·helloworld+0(SB)/8,$text<>(SB) // StringHeader.Data
DATA ·helloworld+8(SB)/8,$12         // StringHeader.Len
DATA ·helloworld+16(SB)/8,$16        // StringHeader.Cap

GLOBL text<>(SB),$16
DATA text<>+0(SB)/8,$"Hello Wo"      // ...string data...
DATA text<>+8(SB)/8,$"rld!"          // ...string data...
```

## map/channel 类型变量

map/channel 等类型并没有公开的内部结构，它们只是一种未知类型的指针，无法直接初始化。在汇编代码中我们只能为类似变量定义并进行 0 值初始化

```go
var m map[string]int

var ch chan int

GLOBL ·m(SB),$8  // var m map[string]int
DATA  ·m+0(SB)/8,$0

GLOBL ·ch(SB),$8 // var ch chan int
DATA  ·ch+0(SB)/8,$0


//其实在runtime包中为汇编提供了一些辅助函数。比如在汇编中可以通过runtime.makemap和runtime.makechan内部函数来创建map和chan变量。辅助函数的签名如下：

func makemap(mapType *byte, hint int, mapbuf *any) (hmap map[any]any)
func makechan(chanType *byte, size int) (hchan chan any)
```

## 标识符规则和特殊标志

Go 语言的标识符可以由绝对的包路径加标识符本身定位，因此不同包中的标识符即使同名也不会有问题。Go 汇编是通过特殊的符号来表示斜杠和点符号，因为这样可以简化汇编器词法扫描部分代码的编写，只要通过字符串替换就可以了。

此外，Go 汇编语言还在"textflag.h"文件定义了一些标志。其中用于变量的标志有 DUPOK、RODATA 和 NOPTR 几个。`DUPOK` 表示该变量对应的标识符可能有多个，在链接时只选择其中一个即可（一般用于合并相同的常量字符串，减少重复数据占用的空间）。`RODATA` 标志表示将变量定义在只读内存段，因此后续任何对此变量的修改操作将导致异常（recover 也无法捕获）。`NOPTR` 则表示此变量的内部不含指针数据，让垃圾回收器忽略对该变量的扫描。如果变量已经在 Go 代码中声明过的话，Go 编译器会自动分析出该变量是否包含指针，这种时候可以不用手写 NOPTR 标志。

```go
#include "textflag.h"

GLOBL ·const_id(SB),NOPTR|RODATA,$8
DATA  ·const_id+0(SB)/8,$9527
```

# 函数

函数标识符通过 TEXT 汇编指令定义，表示该行开始的指令定义在 TEXT 内存段。TEXT 语句后的指令一般对应函数的实现，但是对于 TEXT 指令本身来说并不关心后面是否有指令。因此 TEXT 和 LABEL 定义的符号是类似的，区别只是 LABEL 是用于跳转标号，但是本质上他们都是通过标识符映射一个内存地址。

```go
/**
其中TEXT用于定义函数符号，函数名中当前包的路径可以省略。
函数的名字后面是(SB)，表示是函数名符号相对于SB伪寄存器的偏移量，二者组合在一起最终是绝对地址。作为全局的标识符的全局变量和全局函数的名字一般都是基于SB伪寄存器的相对地址。
标志部分用于指示函数的一些特殊行为，标志在textlags.h文件中定义，常见的NOSPLIT主要用于指示叶子函数不进行栈分裂。
framesize部分表示函数的局部变量需要多少栈空间，其中包含调用其它函数时准备调用参数的隐式栈空间。
最后是可以省略的参数大小，之所以可以省略是因为编译器可以从Go语言的函数声明中推导出函数参数的大小。
*/
TEXT symbol(SB), [flags,] $framesize[-argsize]

//go:nosplit
func Swap(a, b int) (int, int)
// func Swap(a, b int) (int, int)
TEXT ·Swap(SB), NOSPLIT, $0-32
// func Swap(a, b int) (int, int)
TEXT ·Swap(SB), NOSPLIT, $0


//对于函数来说，最重要的是函数对外提供的API约定，包含函数的名称、参数和返回值。当这些都确定之后，如何精确计算参数和返回值的大小是第一个需要解决的问题。
func Swap(a, b int) (ret0, ret1 int)// 对于这个函数，我们可以轻易看出它需要4个int类型的空间，参数和返回值的大小也就是32个字节：
TEXT ·Swap(SB), $0-32
//那么如何在汇编中引用这4个参数呢？为此Go汇编中引入了一个FP伪寄存器，表示函数当前帧的地址，也就是第一个参数的地址。因此我们以通过+0(FP)、+8(FP)、+16(FP)和+24(FP)来分别引用a、b、ret0和ret1四个参数。
//但是在汇编代码中，我们并不能直接以+0(FP)的方式来使用参数。为了编写易于维护的汇编代码，Go汇编语言要求，任何通过FP伪寄存器访问的变量必和一个临时标识符前缀组合后才能有效，一般使用参数对应的变量名作为前缀。

TEXT ·Swap(SB), $0
    MOVQ a+0(FP), AX     // AX = a
    MOVQ b+8(FP), BX     // BX = b
    MOVQ BX, ret0+16(FP) // ret0 = BX
    MOVQ AX, ret1+24(FP) // ret1 = AX
    RET



func Foo(a bool, b int16) (c []byte)

TEXT ·Foo(SB), $0
    MOVEQ a+0(FP),       AX // a
    MOVEQ b+2(FP),       BX // b a和b参数之间出现了一个字节的空洞
    MOVEQ c_dat+8*1(FP), CX // c.Data  b和c之间出现了4个字节的空洞
    MOVEQ c_len+8*2(FP), DX // c.Len
    MOVEQ c_cap+8*3(FP), DI // c.Cap
    RET
    //出现空洞的原因是要保证每个参数变量地址都要对齐到相应的倍数。

// 函数内局部变量
func Foo() {
    var c []byte
    var b int16
    var a bool
}
TEXT ·Foo(SB), $32-0
    MOVQ a-32(SP),      AX // a
    MOVQ b-32(SP),      BX // b
    MOVQ c_data-24(SP), CX // c.Data
    MOVQ c_len-16(SP),  DX // c.Len
    MOVQ c_cap-8(SP),   DI // c.Cap
    RET
```

# 流程控制

## if

```go
func If(ok int, a, b int) int {
    if ok == 0 { goto L }
    return a
L:
    return b
}
TEXT ·If(SB), NOSPLIT, $0-32
    MOVQ ok+8*0(FP), CX // ok
    MOVQ a+8*1(FP), AX  // a
    MOVQ b+8*2(FP), BX  // b

    CMPQ CX, $0         // test ok
    JZ   L              // if ok == 0, goto L
    MOVQ AX, ret+24(FP) // return a
    RET

L:
    MOVQ BX, ret+24(FP) // return b
    RET
```

## for

```go
func LoopAdd(cnt, v0, step int) int {
    result := v0
    for i := 0; i < cnt; i++ {
        result += step
    }
    return result
}

func LoopAdd(cnt, v0, step int) int {
    var i = 0
    var result = 0

LOOP_BEGIN:
    result = v0

LOOP_IF:
    if i < cnt { goto LOOP_BODY }
    goto LOOP_END

LOOP_BODY
    i = i+1
    result = result + step
    goto LOOP_IF

LOOP_END:

    return result
}


#include "textflag.h"

// func LoopAdd(cnt, v0, step int) int
TEXT ·LoopAdd(SB), NOSPLIT,  $0-32
    MOVQ cnt+0(FP), AX   // cnt
    MOVQ v0+8(FP), BX    // v0/result
    MOVQ step+16(FP), CX // step

LOOP_BEGIN:
    MOVQ $0, DX          // i

LOOP_IF:
    CMPQ DX, AX          // compare i, cnt
    JL   LOOP_BODY       // if i < cnt: goto LOOP_BODY
    JMP LOOP_END

LOOP_BODY:
    ADDQ $1, DX          // i++
    ADDQ CX, BX          // result += step
    JMP LOOP_IF

LOOP_END:

    MOVQ BX, ret+24(FP)  // return result
    RET
```
